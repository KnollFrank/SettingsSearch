apply plugin: 'com.android.application'

android {
    namespace "de.KnollFrank.settingssearch"
    compileSdk 34

    defaultConfig {
        applicationId "de.KnollFrank.settingssearch"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "GENERATE_DATABASE_FOR_ASSET", "false"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
    buildFeatures {
        viewBinding true
        buildConfig = true
    }
}

if (project.hasProperty('generateDb') && project.property('generateDb') == 'true') {
    println ">>> '-PgenerateDb=true' detected. Configuring 'debug' build for DB generation."
    android.buildTypes.debug.buildConfigField "boolean", "GENERATE_DATABASE_FOR_ASSET", "true"
}

tasks.register('installForDbGeneration') {
    group = "Database"
    description = "Builds and installs the debug app and test app."
    dependsOn 'installDebug', 'installDebugAndroidTest'
}

// execute this task using: ./gradlew generatePrepackagedDatabase -PgenerateDb=true
tasks.register('generatePrepackagedDatabase', Exec) {
    group = "Database"
    description = "Runs the test, copies the database, and cleans up. Must be called with '-PgenerateDb=true'."
    dependsOn tasks.named('installForDbGeneration')
    ext {
        // dbNameOnDevice MUST match PreferencesDatabaseFactory.createPreferencesDatabaseConfigUsingPrepackagedDatabaseAssetFile().databaseFileName()
        dbNameOnDevice = "searchable_preferences.db"
        // assetPathAndName MUST match PreferencesDatabaseFactory.createPreferencesDatabaseConfigUsingPrepackagedDatabaseAssetFile().prepackagedPreferencesDatabase().get().databaseAssetFile().getPath()
        assetPathAndName = "database/searchable_preferences_prepackaged.db"
        testClassName = "de.KnollFrank.settingssearch.GenerateDatabaseTest"
    }
    workingDir project.rootDir
    commandLine 'sh', '-c', """
        set -e
        appPackage="${android.defaultConfig.applicationId}"
        adb="${android.sdkDirectory}/platform-tools/adb"
        dbNameOnDevice="${ext.dbNameOnDevice}"
        assetPathAndName="${ext.assetPathAndName}"
        testClassName="${ext.testClassName}"
        deviceDbPath="/data/data/\${appPackage}/databases/\${dbNameOnDevice}"
        absoluteLocalAssetPath="${project.file("src/main/assets/\${assetPathAndName}")}"

        mkdir -p "\$(dirname "\${absoluteLocalAssetPath}")"
        echo ">>> Apps installed. Starting test to create database..."
        INSTRUMENTATION_OUTPUT=\$("\${adb}" shell am instrument -w -e class \${testClassName} \${appPackage}.test/androidx.test.runner.AndroidJUnitRunner 2>&1)
        echo "--- Test Output Start ---"
        echo "\${INSTRUMENTATION_OUTPUT}"
        echo "--- Test Output End ---"
        if echo "\${INSTRUMENTATION_OUTPUT}" | grep -q -E 'INSTRUMENTATION_CODE: 0|FAILURES!!!'; then
            echo
            echo "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
            echo "!!! BUILD FAILED: Test run crashed or reported a failure. !!!"
            echo "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
            exit 1
        fi
        echo ">>> Test finished. Pulling database from device to assets folder..."
        "\${adb}" exec-out run-as \${appPackage} cat "databases/\${dbNameOnDevice}" > "\${absoluteLocalAssetPath}"
        echo ">>> Cleaning up: Uninstalling apps..."
        "\${adb}" uninstall "\${appPackage}"
        "\${adb}" uninstall "\${appPackage}.test"
        echo ">>> Database update process completed successfully!"
    """
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.guava:guava:33.5.0-android'
    implementation project(":lib")
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'org.jgrapht:jgrapht-io:1.5.2'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'org.mockito:mockito-android:5.7.0'
    androidTestImplementation 'org.mockito:mockito-core:5.7.0'
    androidTestImplementation group: 'com.github.npathai', name: 'hamcrest-optional', version: '2.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.6.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.6.1'
    implementation 'com.codepoetics:ambivalence:0.2'

    def room_version = "2.8.1"
    androidTestImplementation "androidx.room:room-runtime:$room_version"
    androidTestImplementation "androidx.room:room-runtime-android:$room_version"
    androidTestAnnotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    testImplementation "androidx.room:room-runtime:$room_version"
    testImplementation "androidx.room:room-runtime-android:$room_version"
    testAnnotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
}
